1 REM ===== CRAZY SNAKE =====
2 REM
3 REM WRITTEN FOR : COMMANDER X16
4 REM TESTED ON   : EMULATOR R.43
5 REM AUTHOR      : DUSAN STRAKL
6 REM MORE INFO   : 8BITCODING.COM
7 REM
10 DIM X%(5000):DIM Y%(5000):DIM C%(5000)
20 SC=100000000:LV=1:OVER=0
30 GOSUB 9000
40 GET A$:IF A$="" GOTO 40
50 X%(0)=40:X%(1)=40:X%(2)=40:X%(3)=40:X%(4)=40
60 Y%(0)=30:Y%(1)=31:Y%(2)=32:Y%(3)=33:Y%(4)=34
70 C%(0)=66:C%(1)=66:C%(2)=66:C%(3)=66:C%(4)=66
80 T%=0:H%=4:TM=4
90 D%=1:DX%=0:DY%=1
100 HX%=0:HY%=0
110 GROW=0:COUNT=0
120 GOSUB 6000
130 GOSUB 6500
140 GOSUB 3500
150 FOR N=1 TO 10:GET A$:NEXT N
499 REM
500 REM ===== GAME LOOP =====
501 REM
510 GET A$
520 IF A$=CHR$(157) THEN GOSUB 1000
530 IF A$=CHR$(29) THEN GOSUB 1500
540 FOR W=1 TO (5-LV)*30:NEXT W
550 GOSUB 2000
560 GOSUB 3000
570 GOSUB 4000
580 TM=TM+1:VPOKE 1,$B000+74+INT((5000-TM)/250)*2,$60
590 IF TM<5000 GOTO 650
600 IF LV=5 THEN OVER=1: GOTO 660
610 GOSUB 6700
620 IF COUNT<10 THEN OVER=1:GOTO 660
630 LV=LV+1
640 GOTO 50
650 IF OVER=0 GOTO 510
660 GOSUB 7500
670 GOTO 20
999 REM
1000 REM ===== TURN LEFT =====
1001 REM
1010 IF D%=1 THEN D%=4:DX%=1:DY%=0:C%(H%)=74:RETURN
1020 IF D%=2 THEN D%=1:DX%=0:DY%=1:C%(H%)=85:RETURN
1030 IF D%=3 THEN D%=2:DX%=-1:DY%=0:C%(H%)=73:RETURN
1040 IF D%=4 THEN D%=3:DX%=0:DY%=-1:C%(H%)=75:RETURN
1499 REM
1500 REM ===== TURN RIGHT =====
1501 REM
1510 IF D%=1 THEN D%=2:DX%=-1:DY%=0:C%(H%)=75:RETURN
1520 IF D%=2 THEN D%=3:DX%=0:DY%=-1:C%(H%)=74:RETURN
1530 IF D%=3 THEN D%=4:DX%=1:DY%=0:C%(H%)=85:RETURN
1540 IF D%=4 THEN D%=1:DX%=0:DY%=1:C%(H%)=73:RETURN
1999 REM
2000 REM ===== UPDATE SNAKE =====
2001 REM
2010 H%=H%+1
2020 IF GROW=0 THEN T%=T%+1
2030 IF GROW>0 THEN GROW=GROW-1
2040 X%(H%)=X%(H%-1)+DX%
2050 Y%(H%)=Y%(H%-1)+DY%
2060 C%(H%)=64
2070 IF D%=1 OR D%=3 THEN C%(H%)=66
2080 RETURN
2999 REM
3000 REM ===== COLLISION CONTROL =====
3001 REM
3010 H=VPEEK(1,$B000+Y%(H%)*256+X%(H%)*2)
3020 IF H=32 THEN RETURN
3030 IF H=83 THEN GOTO 3100
3040 OVER=1
3050 GOSUB 7000
3060 RETURN
3100 GROW=GROW+LV*5
3110 SC=SC+LV*5:COUNT=COUNT+1
3120 VPOKE 1,$B000+Y%(H%)*256+X%(H%)*2+1,$B7
3130 PRINT CHR$(19);
3140 PRINT TAB(11)+RIGHT$(STR$(SC),8)
3150 GOSUB 3500
3160 RETURN
3499 REM
3500 REM ===== SPAWN NEW HEART =====
3501 REM
3510 HX%=INT(RND(1)*78+1)
3520 HY%=INT(RND(1)*58+1)
3530 IF VPEEK(1,$B000+HY%*256+HX%*2)<>32 THEN GOTO 3510
3540 VPOKE 1,$B000+HY%*256+HX%*2,83
3550 VPOKE 1,$B000+HY%*256+HX%*2+1,$B8
3560 RETURN
3999 REM
4000 REM ===== DRAW SNAKE =====
4001 REM
4010 VPOKE 1,$B000+Y%(H%)*256+X%(H%)*2,90
4020 VPOKE 1,$B000+Y%(H%-1)*256+X%(H%-1)*2,C%(H%-1)
4030 VPOKE 1,$B000+Y%(T%)*256+X%(T%)*2,32
4040 RETURN
4999 REM
5000 REM ===== DRAW WHOLE SNAKE =====
5001 REM
5010 FOR N=T% TO H%
5020 VPOKE 1,$B000+Y%(N)*256+X%(N)*2,1
5030 NEXT N
5040 RETURN
5499 REM
5500 REM ===== DRAW DEAD SNAKE =====
5501 REM
5510 FOR N=T% TO H%
5520 VPOKE 1,$B000+Y%(N)*256+X%(N)*2,24
5530 NEXT N
5540 RETURN
5999 REM
6000 REM ===== CLEAR SCREEN =====
6001 REM
6010 SCREEN 0:COLOR 7,11:CLS
6020 FOR X=0 TO 79:VPOKE 1,$B000+X*2+1,$0E:VPOKE 1,$B000+X*2,$60
6030 VPOKE 1,$B000+15104+X*2+1,$E0:VPOKE 1,$B000+15104+X*2,$60:NEXT X
6040 FOR Y=0 TO 59:VPOKE 1,$B000+Y*256+1,$0E:VPOKE 1,$B000+Y*256,$60
6050 VPOKE 1,$B000+Y*256+159,$E0:VPOKE 1,$B000+Y*256+158,$60:NEXT Y
6060 VPOKE 1,$B000+158,105
6070 VPOKE 1,$B000+15105,$E0:VPOKE 1,$B000+15104,105
6080 PRINT CHR$(19);
6090 PRINT TAB(5)+"SCORE:";
6100 PRINT TAB(30)+"TIME:\XA0\XC3\XC3\XC3\XC3\XC3\XC3\XC3\XC3\XC3\XC3\XC3\XC3\XC3\XC3\XC3\XC3\XC3\XC3\XC3\XC3";
6110 PRINT TAB(65)+"LEVEL:"
6120 FOR X=5 TO 70:VPOKE 1,$B000+X*2+1,$0E:NEXT X
6130 PRINT CHR$(19);
6140 PRINT TAB(11)+RIGHT$(STR$(SC),8)
6150 PRINT CHR$(19);:PRINT TAB(72)+RIGHT$(STR$(LV),1)
6160 RETURN
6499 REM
6500 REM ===== READY MESSAGE =====
6501 REM
6510 PRINT CHR$(19)
6520 PRINT "\X11\X11\X11\X11\X11\X11\X11\X11\X11\X11\X11\X11\X11\X11\X11\X11\X11\X11\X11\X11\X11\X11\X11\X11\X11"
6530 PRINT TAB(36)+CHR$(154)+"LEVEL:"+STR$(LV)
6540 PRINT:PRINT
6550 PRINT TAB(30)+CHR$(129)+"G E T    R E A D Y !"
6560 FOR N=1 TO 5000:NEXT N
6570 PRINT "\X91\X91\X91\X91\X91"
6580 PRINT TAB(36)+CHR$(158)+"        "
6590 PRINT:PRINT
6600 PRINT TAB(30)+CHR$(158)+"                    "
6610 RETURN
6699 REM
6700 REM ===== TIME'S UP MESSAGE =====
6701 REM
6710 PRINT CHR$(19)
6720 PRINT "\X11\X11\X11\X11\X11\X11\X11\X11\X11\X11\X11\X11\X11\X11\X11\X11\X11\X11\X11\X11\X11\X11\X11"
6730 IF COUNT>=10 THEN 6760
6740 PRINT TAB(25)+CHR$(154)+"TIME IS UP, NOT ENOUGH HEARTS!"
6750 RETURN
6760 PRINT TAB(35)+CHR$(154)+"TIME IS UP!"
6770 FOR N=1 TO 2000:NEXT N
6780 RETURN
6999 REM
7000 REM ===== DEAD SNAKE MESSAGE =====
7001 REM
7010 GOSUB 5500
7020 PRINT CHR$(19)
7030 PRINT "\X11\X11\X11\X11\X11\X11\X11\X11\X11\X11\X11\X11\X11\X11\X11\X11\X11\X11\X11\X11\X11\X11\X11"
7040 IF X%(H%)=0 OR X%(H%)=79 OR Y%(H%)=0 OR Y%(H%)=59 THEN GOTO 7070
7050 PRINT TAB(23)+CHR$(154)+"YIKES YOU SHOULDN'T BITE YOURSELF"
7060 GOTO 7100
7070 PRINT TAB(24)+CHR$(154)+"OUCH, YOU HIT THE WALL, IT HURTS"
7100 PRINT:PRINT
7110 REM PRINT TAB(30)+CHR$(150)+"G A M E     O V E R"
7120 RETURN
7499 REM
7500 REM ===== GAME OVER MESSAGE =====
7501 REM
7510 PRINT CHR$(19)
7520 PRINT "\X11\X11\X11\X11\X11\X11\X11\X11\X11\X11\X11\X11\X11\X11\X11\X11\X11\X11\X11\X11\X11\X11\X11\X11\X11"
7530 PRINT TAB(30)+CHR$(154)+"G A M E     O V E R"
7540 PRINT:PRINT
7550 PRINT TAB(32)+CHR$(150)+"SCORE: "+RIGHT$(STR$(SC),8)
7560 FOR N=1 TO 10:GET A$:NEXT N
7570 GET A$:IF A$="" GOTO 7570
7580 RETURN
8999 REM
9000 REM ===== TITLE SCREEN =====
9001 REM
9005 COLOR 7,11:CLS:PRINT
9010 PRINT TAB(10)+CHR$(18);
9015 PRINT "\X1D\X1D\X1D     \X1D\X1D\X1D\X1D\X1D       \X1D\X1D\X1D\X1D\X1D\X1D\X1D       \X1D\X1D\X1D\X1D         \X1D    \X1D\X1D    "
9020 PRINT TAB(10)+CHR$(18);
9025 PRINT "\X1D         \X1D\X1D\X1D          \X1D\X1D\X1D\X1D       \X1D\X1D\X1D\X1D         \X1D\X1D   \X1D\X1D   "
9030 PRINT TAB(10)+CHR$(18);
9035 PRINT "\X1D         \X1D\X1D\X1D          \X1D\X1D\X1D\X1D       \X1D\X1D\X1D\X1D         \X1D\X1D   \X1D\X1D   "
9040 PRINT TAB(10)+CHR$(18);
9045 PRINT "    \X1D\X1D    \X1D\X1D\X1D    \X1D\X1D    \X1D\X1D\X1D\X1D   \X1D   \X1D\X1D\X1D\X1D         \X1D\X1D   \X1D\X1D   "
9050 PRINT TAB(10)+CHR$(18);
9055 PRINT "    \X1D\X1D    \X1D\X1D\X1D    \X1D\X1D    \X1D\X1D\X1D\X1D   \X1D   \X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D    \X1D\X1D   \X1D\X1D   "
9060 PRINT TAB(10)+CHR$(18);
9065 PRINT "    \X1D\X1D    \X1D\X1D\X1D    \X1D\X1D    \X1D\X1D\X1D\X1D   \X1D   \X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D    \X1D\X1D\X1D   \X1D\X1D   "
9070 PRINT TAB(10)+CHR$(18);
9075 PRINT "    \X1D\X1D    \X1D\X1D\X1D          \X1D\X1D\X1D    \X1D    \X1D\X1D\X1D\X1D\X1D\X1D\X1D    \X1D\X1D\X1D\X1D  \X1D\X1D  "
9080 PRINT TAB(10)+CHR$(18);
9085 PRINT "    \X1D\X1D    \X1D\X1D\X1D         \X1D\X1D\X1D\X1D    \X1D    \X1D\X1D\X1D\X1D\X1D\X1D    \X1D\X1D\X1D\X1D\X1D      "
9090 PRINT TAB(10)+CHR$(18);
9095 PRINT "    \X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D        \X1D\X1D\X1D\X1D\X1D    \X1D    \X1D\X1D\X1D\X1D\X1D\X1D    \X1D\X1D\X1D\X1D\X1D      "
9100 PRINT TAB(10)+CHR$(18);
9105 PRINT "    \X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D         \X1D\X1D\X1D\X1D    \X1D    \X1D\X1D\X1D\X1D\X1D\X1D    \X1D\X1D\X1D\X1D\X1D      "
9110 PRINT TAB(10)+CHR$(18);
9115 PRINT "    \X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D    \X1D\X1D    \X1D\X1D\X1D   \X1D\X1D\X1D   \X1D\X1D\X1D\X1D\X1D    \X1D\X1D\X1D\X1D\X1D\X1D\X1D    "
9120 PRINT TAB(10)+CHR$(18);
9125 PRINT "    \X1D\X1D    \X1D\X1D\X1D    \X1D\X1D    \X1D\X1D\X1D   \X1D\X1D\X1D   \X1D\X1D\X1D\X1D\X1D    \X1D\X1D\X1D\X1D\X1D\X1D\X1D    "
9130 PRINT TAB(10)+CHR$(18);
9135 PRINT "    \X1D\X1D    \X1D\X1D\X1D    \X1D\X1D    \X1D\X1D\X1D   \X1D\X1D\X1D   \X1D\X1D\X1D\X1D    \X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D    "
9140 PRINT TAB(10)+CHR$(18);
9145 PRINT "    \X1D\X1D    \X1D\X1D\X1D    \X1D\X1D    \X1D\X1D\X1D         \X1D\X1D\X1D\X1D    \X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D    "
9150 PRINT TAB(10)+CHR$(18);
9155 PRINT "    \X1D\X1D    \X1D\X1D\X1D    \X1D\X1D    \X1D\X1D\X1D         \X1D\X1D\X1D\X1D    \X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D    "
9160 PRINT TAB(10)+CHR$(18);
9165 PRINT "    \X1D\X1D    \X1D\X1D\X1D    \X1D\X1D    \X1D\X1D\X1D         \X1D\X1D\X1D         \X1D\X1D\X1D\X1D    "
9170 PRINT TAB(10)+CHR$(18);
9175 PRINT "\X1D        \X1D\X1D\X1D\X1D    \X1D\X1D    \X1D\X1D    \X1D\X1D\X1D    \X1D\X1D         \X1D\X1D\X1D\X1D    "
9180 PRINT TAB(10)+CHR$(18);
9185 PRINT "\X1D        \X1D\X1D\X1D\X1D    \X1D\X1D    \X1D\X1D    \X1D\X1D\X1D    \X1D\X1D         \X1D\X1D\X1D\X1D    "
9190 PRINT TAB(10)+CHR$(18);
9195 PRINT "\X1D\X1D     \X1D\X1D\X1D\X1D\X1D\X1D    \X1D\X1D    \X1D\X1D    \X1D\X1D\X1D    \X1D\X1D         \X1D\X1D\X1D\X1D    "
9200 PRINT:PRINT
9210 PRINT TAB(10)+CHR$(18);
9215 PRINT "\X1D\X1D      \X1D\X1D\X1D\X1D\X1D    \X1D\X1D\X1D   \X1D\X1D\X1D\X1D       \X1D\X1D\X1D\X1D    \X1D\X1D\X1D    \X1D\X1D       "
9220 PRINT TAB(10)+CHR$(18);
9225 PRINT "\X1D        \X1D\X1D\X1D\X1D    \X1D\X1D\X1D   \X1D\X1D\X1D\X1D       \X1D\X1D\X1D\X1D    \X1D\X1D\X1D    \X1D\X1D       "
9230 PRINT TAB(10)+CHR$(18);
9235 PRINT "          \X1D\X1D\X1D    \X1D\X1D\X1D   \X1D\X1D\X1D\X1D       \X1D\X1D\X1D\X1D    \X1D\X1D    \X1D\X1D\X1D       "
9240 PRINT TAB(10)+CHR$(18);
9245 PRINT "    \X1D\X1D    \X1D\X1D\X1D     \X1D\X1D   \X1D\X1D\X1D\X1D   \X1D   \X1D\X1D\X1D\X1D    \X1D\X1D    \X1D\X1D\X1D       "
9250 PRINT TAB(10)+CHR$(18);
9255 PRINT "    \X1D\X1D    \X1D\X1D\X1D     \X1D\X1D   \X1D\X1D\X1D\X1D   \X1D   \X1D\X1D\X1D\X1D    \X1D    \X1D\X1D\X1D\X1D    "
9260 PRINT TAB(10)+CHR$(18);
9265 PRINT "    \X1D\X1D    \X1D\X1D\X1D     \X1D\X1D   \X1D\X1D\X1D\X1D   \X1D   \X1D\X1D\X1D\X1D    \X1D    \X1D\X1D\X1D\X1D    "
9270 PRINT TAB(10)+CHR$(18);
9275 PRINT "     \X1D\X1D\X1D\X1D\X1D\X1D\X1D\X1D      \X1D   \X1D\X1D\X1D    \X1D    \X1D\X1D\X1D        \X1D\X1D\X1D\X1D\X1D    "
9280 PRINT TAB(10)+CHR$(18);
9285 PRINT "       \X1D\X1D\X1D\X1D\X1D\X1D      \X1D   \X1D\X1D\X1D    \X1D    \X1D\X1D\X1D        \X1D\X1D\X1D\X1D\X1D       "
9290 PRINT TAB(10)+CHR$(18);
9295 PRINT "\X1D       \X1D\X1D\X1D\X1D\X1D      \X1D   \X1D\X1D\X1D    \X1D    \X1D\X1D\X1D       \X1D\X1D\X1D\X1D\X1D\X1D       "
9300 PRINT TAB(10)+CHR$(18);
9310 PRINT TAB(10)+CHR$(18);
9315 PRINT "\X1D\X1D\X1D\X1D      \X1D\X1D\X1D   \X1D      \X1D\X1D\X1D   \X1D\X1D\X1D   \X1D\X1D\X1D        \X1D\X1D\X1D\X1D\X1D       "
9320 PRINT TAB(10)+CHR$(18);
9325 PRINT "\X1D\X1D\X1D\X1D\X1D\X1D     \X1D\X1D   \X1D      \X1D\X1D\X1D   \X1D\X1D\X1D   \X1D\X1D\X1D        \X1D\X1D\X1D\X1D\X1D    "
9330 PRINT TAB(10)+CHR$(18);
9335 PRINT "    \X1D\X1D\X1D    \X1D\X1D   \X1D      \X1D\X1D\X1D   \X1D\X1D\X1D   \X1D\X1D\X1D    \X1D    \X1D\X1D\X1D\X1D    "
9340 PRINT TAB(10)+CHR$(18);
9345 PRINT "    \X1D\X1D\X1D    \X1D\X1D   \X1D\X1D     \X1D\X1D\X1D         \X1D\X1D\X1D    \X1D    \X1D\X1D\X1D\X1D    "
9350 PRINT TAB(10)+CHR$(18);
9355 PRINT "    \X1D\X1D\X1D    \X1D\X1D   \X1D\X1D     \X1D\X1D\X1D         \X1D\X1D\X1D    \X1D\X1D    \X1D\X1D\X1D    "
9360 PRINT TAB(10)+CHR$(18);
9365 PRINT "    \X1D\X1D\X1D    \X1D\X1D   \X1D\X1D     \X1D\X1D           \X1D\X1D    \X1D\X1D    \X1D\X1D\X1D        "
9370 PRINT TAB(10)+CHR$(18);
9375 PRINT "           \X1D\X1D   \X1D\X1D\X1D    \X1D\X1D    \X1D\X1D\X1D    \X1D\X1D    \X1D\X1D    \X1D\X1D\X1D        "
9380 PRINT TAB(10)+CHR$(18);
9385 PRINT "\X1D         \X1D\X1D\X1D   \X1D\X1D\X1D    \X1D\X1D    \X1D\X1D\X1D    \X1D\X1D    \X1D\X1D\X1D    \X1D\X1D        "
9390 PRINT TAB(10)+CHR$(18);
9395 PRINT "\X1D\X1D\X1D     \X1D\X1D\X1D\X1D\X1D   \X1D\X1D\X1D    \X1D\X1D    \X1D\X1D\X1D    \X1D\X1D    \X1D\X1D\X1D    \X1D\X1D        "
9400 PRINT:PRINT CHR$(5)
9410 PRINT TAB(10)+"COLLECT AS MANY HEARTS AS YOU CAN IN 5 TIME LIMITED LEVELS"
9420 PRINT TAB(10)+"YOU MUST COLLECT AT LEAST 10 IN EACH LEVEL TO MOVE TO NEXT"
9430 PRINT TAB(10)+"USE LEFT AND RIGHT ARROW TO TURN FROM SNAKE'S PERSPECTIVE."
9440 PRINT CHR$(154)
9450 PRINT TAB(22)+"POINTS: LEVEL 1 -  5 POINTS"
9460 PRINT TAB(22)+"        LEVEL 2 - 10 POINTS"
9470 PRINT TAB(22)+"        LEVEL 3 - 15 POINTS"
9480 PRINT TAB(22)+"        LEVEL 4 - 20 POINTS"
9490 PRINT TAB(22)+"        LEVEL 5 - 25 POINTS"
9500 PRINT:PRINT CHR$(129)
9510 PRINT TAB(28)+"PRESS ANY KEY TO START!"
9520 PRINT:PRINT CHR$(154)
9530 PRINT TAB(20)+"WRITTEN BY DUSAN STRAKL - DECEMBER 2019"
9540 RETURN