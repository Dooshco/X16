1 REM ===== OPTIONS LIBRARY =====
2 REM
3 REM WRITTEN FOR : COMMANDER X16
4 REM TESTED ON   : EMULATOR R.38
5 REM AUTHOR      : DUSAN STRAKL
6 REM MORE INFO   : 8BITCODING.COM
7 REM
8 REM
10 DIM OX(4,3):DIM OY(4,3):DIM OT$(4,3):DIM OS(4,3):DIM RV(4,3)
20 GOSUB 11000
30 CLS:CM=1:C=0
40 GOSUB 10000
50 IF RET=7 THEN END
60 IF RET=4 THEN PRINT"\X13MUSIC IS ON "
70 IF RET=5 THEN PRINT"\X13MUSIC IS OFF"
80 GOTO 40
9999 REM
10000 REM ===== DISPLAY OPTIONS =====
10001 REM
10010 COLOR DC,DB
10020 FOR N=1 TO OC:FOR M=1 TO OC(N)
10030 IF OS(N,M)=1 THEN COLOR SC,SB
10040 IF OS(N,M)=0 THEN COLOR DC,DB
10050 POKE $30D,OY(N,M):POKE $30E,OX(N,M):SYS$FFF0:PRINT OT$(N,M)
10060 NEXT M,N
10099 REM
10100 REM ===== PROCESS OPTIONS =====
10101 REM
10110 FOR N=1 TO 10:GET A$:NEXT N
10120 IF C<>0 THEN GOTO 10145
10125 C=1
10130 IF OS(CM,C)=0 AND C<=OC(CM) THEN C=C+1:GOTO 10130
10140 IF C>OC(CM) THEN C=1
10145 COLOR AC,AB
10150 POKE $30D,OY(CM,C):POKE $30E,OX(CM,C):SYS$FFF0:PRINT OT$(CM,C)
10160 GET A$:IF A$="" THEN GOTO 10160
10170 A=ASC(A$)
10200 IF A=32 OR A=13 THEN GOTO 10300
10210 IF A=157 THEN C=C-1:IF C=0 THEN C=OC(CM)
10220 IF A=29 THEN C=C+1:IF C>OC(CM) THEN C=1
10230 IF A=17 THEN C=0:CM=CM+1:IF CM>OC THEN CM=1
10240 IF A=145 THEN C=0:CM=CM-1:IF CM=0 THEN CM=OC
10250 GOTO 10010
10300 RET=RV(CM,C)
10310 FOR N=1 TO OC(CM):OS(CM,N)=0:NEXT N
10320 OS(CM,C)=1
10330 C=0
10340 RETURN
10999 REM
11000 REM ===== CONFIGURE OPTIONS =====
11001 REM
11010 OC=4:OC(1)=3:OC(2)=2:OC(3)=1:OC(4)=1
11020 DC=15:DB=6:SC=1:SB=0:AC=0:AB=1
11030 OX(1,1)=3:OY(1,1)=10:OT$(1,1)="   EASY   ":RV(1,1)=1:OS(1,1)=1
11040 OX(1,2)=15:OY(1,2)=10:OT$(1,2)="  MEDIUM  ":RV(1,2)=2
11050 OX(1,3)=27:OY(1,3)=10:OT$(1,3)="   HARD   ":RV(1,2)=3
11060 OX(2,1)=14:OY(2,1)=14:OT$(2,1)=" ON ":RV(2,1)=4
11070 OX(2,2)=21:OY(2,2)=14:OT$(2,2)=" OFF ":RV(2,2)=5:OS(2,2)=1
11080 OX(3,1)=13:OY(3,1)=17:OT$(3,1)=" INSTRUCTIONS ":RV(3,1)=6
11090 OX(4,1)=17:OY(4,1)=20:OT$(4,1)=" QUIT ":RV(4,1)=7
11100 RETURN
